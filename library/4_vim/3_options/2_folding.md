Складки
Определение складок
====================

Складки это группы строк, которые могут быть скрыты в одну строку для сокращения документа, а так же в последствии раскрыты в первоначальном состоянии.

Существует несколько методов определения складок в документе. Переход между этими методами отменяет старые складки и пересчитывает новые, кроме перехода к методу manual. Такая логика позволяет сначала определить автоматические складки с помощью других методов, а затем перейти в режим ручных складок, чтобы дополнить их.

Выбрать метод, используемый для определения складок можно с помощью команды `set foldmethod=метод`. Возможные "методы" описаны ниже.

Складки могут быть вложенными, это достигается тем, что для каждой строки документа вычисляется уровень складки - целочисленная величина. Благодаря уровню Vim может определить, какие складки должны быть вложенными.

Пользовательские складки
------------------------

Режим `manual` позволяет определять складки вручную. Для этого используются следующие команды:

* `zf` - выделенный текст (в режиме выделения) становится складкой;
* `zd` - удаление складки, находящейся под курсором.

Складки по отступам
-------------------

Режим `indent` позволяет автоматически определять складки на основании отступов от начала строки. Данный метод делит текущее число отступов на значение опции `shiftwidth` для определения уровня складки данной строки.

Складки по синтаксической подсветке
-----------------------------------

Режим `syntax` позволяет автоматически определять складки через используемые правила подсветки синтаксиса. В данном методе складка определяется для регионов (region), которые имеют опцию `fold`. Ниже приведено определение подсветки синтаксиса региона, который является складкой:

   syn region Comment start=/\/\*/ end=/\*\// fold 

Маркерные складки
-----------------

Режим `marker` позволяет определять складки на основании маркеров, выставленных в тексте. Эти маркеры заданы в опции `foldmarker` и по умолчанию имеют значение `set foldmarker={{{,}}}`.

Ниже приведен пример документа с маркерными складками:

    Без складок
    {{{
      Складки 1 уровня
      {{{
        Складки 2 уровня
      }}}
      Складки 1 уровня
    }}}
    Без складок

Вычисляемые складки
-------------------

Режим `expr` позволяет определять складки и их уровня путем применения к каждой строке пользовательского выражения на языке VimScript. Это выражение должно быть записано в опции `foldexpr`. Здесь может быть использована переменная `v:lnum` для определения номера вычисляемой строки. Выражение должно возвращать целое число, представляющее уровень для вычисляемой строки или один из следующих специальных маркеров:

* -1 - уровень текущей складки должен равняться уровню следующей или предыдущей строки (в зависимости от того, у кого уровень больше);
* = - уровень текущей складки должен быть равен уровню предыдущей;
* a1, a2, ... - на 1, 2, ... уровня больше, чем у предыдущей строки;
* s1, s2, ... - на 1, 2, ... уровня меньше, чем у предыдущей стркои;
* <1, <2, ... - складка с указанным уровнем заканчивается на данной строке;
* >1, >2, ... - складка с указанным уровнем начинается на данной строке.

Ниже приведен пример вычисления уровней складок блоков данных, начинающихся с `/*`, `{` и заканчивающихся `*/`, `}`:

    function! MyFold(n)
      let line = getline(a:n)
      let prevLine = getline(a:n-1)
      if line =~ '\V/*'
        return 'a1'
      endif 

      if line =~ '\V*/'
        return 's1'
      endif

      if prevLine =~ '\V{'
        return 'a1'
      endif 
      if line =~ '\V}'
        return 's1'
      endif
      return '=' 
    endfunction

    set foldenable foldmethod=expr foldexpr=MyFold(v:lnum)

Работа со складками
===================

Для работы со складками используются следующие команды:

* `zo` - открыть складку под курсором;
* `zO` - открыть все складки рекурсивно под курсором;
* `zc` - закрыть одну складку под курсором;
* `zC` - закрыть все складки рекурсивно под курсором;
* `za` - открыть складку под курсором, если она была закрыта, и закрыть в противном случае;
* `zA` - открыть все складки рекурсивно под курсором, если они были закрыты, и закрыть в противном случае;
* `zR` - открыть все складки;
* `zM` - закрыть все складки.
* `zj` - переместить курсор к началу следующей складки;
* `zk` - переместить курсор в конец предыдущей складки.

Опции
=====

Для управления складками кроме перечисленных ниже так же используются следующие опции:

|  Наименование | Тип |            Значения             |                                                                                                   Описание                                                                                                                                                                     |        Пример       |
|---------------|-----|---------------------------------|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|---------------------|
|foldenable     |bool |foldenable, nofoldenable         |Если данная опция отключена, то все складки разворачиваются (отключаются)                                                                                                                                                                                                       |set foldenable       |
|foldopen       |str  |block, all, hor, insert, persent |Опция определяет команды, которые используются для открытия вкладки, на которой установлен курсор. В соответствии со значениями это: стандартные команды открытия складок, любые команды, команды горизонтального позиционирования курсора, команды режима вставки, команда `%` |set foldopen=hor     |
|foldclose      |str  |"", all                          |Если эта опция имеет значение "all", то складки будут закрываться автоматически, если курсор находится не в них                                                                                                                                                                 |set foldclose=all    |
|foldcolumn     |int  |0-12                             |Данная опция задает ширину левого поля, содержащего состояния вкладок документа                                                                                                                                                                                                 |set foldcolumn=2     |
|foldlevel      |int  |                                 |Опция задает уровень, все складки выше которого должны быть закрыты                                                                                                                                                                                                             |set foldlevel=0      |
|foldlevelstart |int  |                                 |Опция задает уровень, все складки выше которого должны быть закрыты в новом документе                                                                                                                                                                                           |set foldlevelstart=1 |
|foldminlines   |int  |                                 |Минимальное число строк, которые могут входить в закрывающуюся складку                                                                                                                                                                                                          |set foldminlines=2   |
|foldnestmax    |int  |1-20                             |Для `foldmethod=indent` и `foldmethod=syntax` определяет максимальное возможное число вложенных складок                                                                                                                                                                         |set foldnestmax=5    |

Опция `foldtext` позволяет определить выражение на языке VimScript, используемое для вычисления заменяющего текста складки. Данное выражение может использовать следующие переменные:

* `v:foldstart` - номер первой строки складки;
* `v:foldend` - номер последней строки складки;
* `v:folddashes` - строка, которая представляет величину уровня глубины складки в виде последовательности дефисов;
* `v:foldlevel` - уровень глубины складки.

Ниже приведен пример выражения, вычисляющего текст складки:

    set foldtext=MyFoldText()
    function MyFoldText()
      let line = getline(v:foldstart)
      let sub = substitute(line, '/\*\|\*/\|{{{\d\=', '', 'g')
      return v:folddashes . sub
    endfunction
